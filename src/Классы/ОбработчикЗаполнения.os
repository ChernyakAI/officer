Перем _Поделка;
Перем _Настройки;

Перем ШаблоныДокументов Экспорт;
Перем НастройкиПодстановки Экспорт;

&Желудь
Процедура ПриСозданииОбъекта(
		&Пластилин Поделка,
		&Пластилин Настройки
	)
	_Настройки = Настройки;
	_Поделка = Поделка;
	ШаблоныДокументов = Новый Массив;
КонецПроцедуры

Процедура ПодготовитьШаблоны() Экспорт

	Шаблоны = НайтиФайлы(ОбъединитьПути(ТекущийКаталог(), _Настройки.КаталогШаблонов()), "*.*");
	Для каждого Шаблон Из Шаблоны Цикл

		ПутьРаспакованный = ОбъединитьПути(ТекущийКаталог(), _Настройки.КаталогВременныхФайлов(), Шаблон.ИмяБезРасширения);

		Если СтрЗаканчиваетсяНа(Шаблон.Имя, ".docx") Тогда
			ФайлДокх = _Поделка.НайтиЖелудь("ФайлДокх");
			ФайлДокх.ПутьКФайлуИсходный = Шаблон.ПолноеИмя;
			ФайлДокх.ПутьРаспакованный = ПутьРаспакованный;
			ШаблоныДокументов.Добавить(ФайлДокх);
		КонецЕсли;
		
		Чтение = Новый ЧтениеZipФайла(Шаблон.ПолноеИмя);
		Чтение.ИзвлечьВсе(ПутьРаспакованный, РежимВосстановленияПутейФайловZIP.НеВосстанавливать);
		Чтение.Закрыть();

	КонецЦикла;

КонецПроцедуры

Процедура ПрочитатьНастройкиПодстановки() Экспорт

	ФайлНастроекПодстановки = _Настройки.ПутьКФайлуНастройкиПодстановки();
	
	ЧтениеЖсон = Новый ЧтениеJSON();
	ЧтениеЖсон.ОткрытьФайл(ФайлНастроекПодстановки);
	НастройкиПодстановки = ПрочитатьJSON(ЧтениеЖсон, Истина);
	ЧтениеЖсон.Закрыть();

КонецПроцедуры

Процедура ЗаполнитьДокументы() Экспорт

	Для каждого Шаблон Из ШаблоныДокументов Цикл
		Шаблон.Заполнить();
	КонецЦикла;

КонецПроцедуры

