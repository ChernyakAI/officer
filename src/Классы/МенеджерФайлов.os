// BSLLS:LatinAndCyrillicSymbolInWord-off

Перем _Поделка;              // само приложение
Перем _Настройки;            // настройки приложения
Перем _ФайлыDocx;            // массив желудей с обрабатываемыми файлами
Перем _НастройкиПодстановки; // массив с настройками для замены в шаблонах

&Лог(Значение = "officer.logger.INFO", УчитыватьИмяКласса = Ложь)
Перем ЛогПриложения; // лог приложения

&Желудь
Процедура ПриСозданииОбъекта(
		&Пластилин Поделка,
		&Пластилин Настройки
	)
	_Настройки = Настройки;
	_Поделка = Поделка;
	_ФайлыDocx = Новый Массив;
	_НастройкиПодстановки = Новый Массив;
КонецПроцедуры

Процедура СоздатьЗаготовкиФайловИзШаблонов() Экспорт

	РаспакованныеШаблоны = Новый СписокЗначений();
	ОтсутствующиеШаблоны = Новый Массив();
	НеизвестныеТипыФайлов = Новый Массив();

	Для каждого Настройка Из _НастройкиПодстановки Цикл
		
		ИмяФайлаШаблона = Настройка.Получить("ИмяФайлаШаблона");
		ИмяВыходногоФайла = Настройка.Получить("ИмяВыходногоФайла");
		Если РаспакованныеШаблоны.НайтиПоЗначению(ИмяФайлаШаблона) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;

		ПутьРаспакованный  = ОбъединитьПути(ТекущийКаталог(), _Настройки.КаталогВременныхФайлов(), ИмяФайлаШаблона);
		ПутьКФайлуШаблона = ОбъединитьПути(ТекущийКаталог(), _Настройки.КаталогШаблонов(), ИмяФайлаШаблона);
		НайденныеФайлы = НайтиФайлы(ПутьКФайлуШаблона);
		Если НайденныеФайлы.Количество() = 0 Тогда
			ОтсутствующиеШаблоны.Добавить(ИмяФайлаШаблона);
			Продолжить;
		КонецЕсли;

		ТипФайла = "";
		Если СтрЗаканчиваетсяНа(ПутьКФайлуШаблона, ".docx") Тогда
			ТипФайла = "docx";
		КонецЕсли;

		ФайлDocx = _Поделка.НайтиЖелудь(ТипФайла);
		Если ФайлDocx = Неопределено Тогда
			НеизвестныеТипыФайлов.Добавить(ИмяФайлаШаблона);
			Продолжить;
		КонецЕсли;

		ФайлDocx.УстановитьПутьКФайлуШаблона(ПутьКФайлуШаблона);
		ФайлDocx.УстановитьПутьКФайлуРаспакованный(ПутьРаспакованный);
		ФайлDocx.УстановитьИмяФайлаШаблона(ИмяФайлаШаблона);
		ФайлDocx.УстановитьИмяФайла(ИмяВыходногоФайла);
		_ФайлыDocx.Добавить(ФайлDocx);
		
		Чтение = Новый ЧтениеZipФайла(ПутьКФайлуШаблона);
		Чтение.ИзвлечьВсе(ПутьРаспакованный, РежимВосстановленияПутейФайловZIP.Восстанавливать);
		Чтение.Закрыть();

		РаспакованныеШаблоны.Добавить(ИмяФайлаШаблона);

	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьФайлы() Экспорт

	Для каждого ФайлDocx Из _ФайлыDocx Цикл
		ФайлDocx.ЗаполнитьФайл();
	КонецЦикла;

КонецПроцедуры

Процедура СохранитьВВыходнойКаталог() Экспорт

	КаталогСохранения = ОбъединитьПути(ТекущийКаталог(), _Настройки.КаталогСохранения());
	СоздатьКаталог(КаталогСохранения);
	КаталогВременныхФайлов = ОбъединитьПути(ТекущийКаталог(), _Настройки.КаталогВременныхФайлов());

	Для каждого Настройка Из _НастройкиПодстановки Цикл
		
		ИмяВыходногоФайла = Настройка.Получить("ИмяВыходногоФайла");
		ПолноеИмяВыходногоФайла = ОбъединитьПути(КаталогСохранения, ИмяВыходногоФайла);
		КаталогРаспакованногоШаблона = ОбъединитьПути(КаталогВременныхФайлов, Настройка.Получить("ИмяФайлаШаблона"));

		Архив = Новый ЗаписьZipФайла(ПолноеИмяВыходногоФайла);
		Архив.Добавить(ОбъединитьПути(КаталогРаспакованногоШаблона, "*.*"),
			РежимСохраненияПутейZIP.СохранятьОтносительныеПути,
			РежимОбработкиПодкаталоговZIP.ОбрабатыватьРекурсивно);
		Архив.Записать();
	КонецЦикла;

КонецПроцедуры

Процедура ПрочитатьНастройкиПодстановки() Экспорт

	ФайлНастроекПодстановки = _Настройки.ПутьКФайлуНастройкиПодстановки();
	ФайлНастроекПодстановки = СтрЗаменить(ФайлНастроекПодстановки, "/", ПолучитьРазделительПути());
	Объект = Новый Файл(ФайлНастроекПодстановки);
	Если Объект.Существует() И Объект.ЭтоФайл() Тогда
		ЧтениеJSON = Новый ЧтениеJSON();
		ЧтениеJSON.ОткрытьФайл(ФайлНастроекПодстановки);
		Попытка
			_НастройкиПодстановки = ПрочитатьJSON(ЧтениеJSON, Истина);
			Текст = СтрШаблон("Инициализация настроек. Прочитано объектов: %1", _НастройкиПодстановки.Количество());
			ЛогПриложения.Информация(Текст);
		Исключение
			Текст = СтрШаблон("Инициализация настроек. Ошибка разбора файла настроек: %1", ФайлНастроекПодстановки);
			ЛогПриложения.Ошибка(Текст);
		КонецПопытки;
		ЧтениеJSON.Закрыть();
	Иначе
		ЛогПриложения.Ошибка(СтрШаблон("Инициализация настроек. Файл не найден: %1", ФайлНастроекПодстановки));
	КонецЕсли;

КонецПроцедуры

Функция НастройкиПодстановки() Экспорт
	Возврат _НастройкиПодстановки;
КонецФункции
